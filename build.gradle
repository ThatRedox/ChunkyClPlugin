apply plugin: 'java'
apply plugin: 'idea'

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url 'https://repo.lemaik.de/'
    }
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

configurations {
    implementation.extendsFrom(provided)
    implementation.extendsFrom(bundled)
}

dependencies {
    provided 'se.llbit:chunky-core:2.5.0-SNAPSHOT'
    provided 'org.apache.commons:commons-math3:3.2'
    provided 'it.unimi.dsi:fastutil:8.4.4'
    bundled 'org.jocl:jocl:2.0.2'
}

jar {
    from {
        configurations.bundled.collect { it.isDirectory() ? it : zipTree(it) }
    }

    processResources.exclude('**/.idea', '**/cmake-build-debug')
}

task buildRust(type: Exec) {
    workingDir "./src/main/native/"
    commandLine "cargo", "build"
}

task packageRust() {
    dependsOn(buildRust)
    doLast {
        copy {
            from "./src/main/native/target/debug/native.dll"
            into "./src/main/resources/dev/thatredox/chunkynative/rust/"
        }
        copy {
            from "./src/main/native/target/debug/native.so"
            into "./src/main/resources/dev/thatredox/chunkynative/rust/"
        }
    }
}

compileJava.dependsOn(packageRust)

defaultTasks 'jar'
